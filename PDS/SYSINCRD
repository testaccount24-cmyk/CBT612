         TITLE 'SYSINCRD - CREATE STC SYSIN SYSINCRD'
**********************************************************************
*  NAME        -   SYSINCRD                                          *
*                                                                    *
*  FUNCTION    -   STC DOES NOT ACCEPT SYSIN INSTREAM DATA. THIS     *
*                  PROGRAM READ PARAMETER AND CREATE TEMP SYSIN FILE *
*                  USED BY THE SUBSEQUENT JOB STEPS. IT IS NO NEED   *
*                  TO CREATE A TEMPORARY FILE FOR AD HOC STARTED     *
*                  TASK.                                             *
*                                                                    *
*  SYNTAX      -   SYSINCRD                                          *
*                                                                    *
*  AUTHOR      -   ALBERT CHENG                                      *
*                                                                    *
*  VERSION     -   Z/OS 13                                           *
*                                                                    *
*  DATE        -   SEPT 2014                                         *
*                                                                    *
*  REGISTERS                                                         *
*    REG 3     CURR PARM LOC USED FOR SRST INSTRUCTION               *
*    REG 4     ADDR OF END OF PARAMETER                              *
*    REG 5     REMAINING LENGTH OF THE PARAMETER                     *
*    REG 7     ADDR OF OUTPARM                                       *
*    REG 8     ADDR OF BEG OF NEXT PARAMETER                         *
*    REG 9     ADDR OF SEMICOLIN RETURN FROM SRST INSTRUCTION        *
*    REG 12    PROGRAM BASE REGISTER                                 *
*                                                                    *
*  INPUT       -   THE PARM PARAMETER. SEMICOLON IS THE DELIMETER OF *
*                  A NEW LINE.                                       *
*                  EXAMPLE : PARM='FIRST REORD;SECOND RECORD'        *
*                                                                    *
*  OUTPUT      -   THE PARMETERS ARE WRITTEN TO DDNAME SYSIN.        *
*                  SYSIN:                                            *
*                  FIRST RECORD                                      *
*                  SECOND RECORD                                     *
*                                                                    *
*     //SYSINCRD EXEC SYSINCRD,PARM='FIRST RECORD,;SECOND RECORD'    *
*     //STEPLIB  DD DISP=SHR,DSN=LOADLIB                             *
*     //SYSIN    DD DSN=&&CC,UNIT=DISK,SPACE=(TRK,1),DISP=(,PASS)    *
*     //LIST     EXEC PGM=IEBGENER                                   *
*     //SYSPRINT DD SYSOUT=*                                         *
*     //SYSUT1   DD DSN=&&CC,DISP=(OLD,DELETE)                       *
*     //SYSUT2   DD SYSOUT=*                                         *
*     //SYSIN    DD DUMMY                                            *
*                                                                    *
* ABSTRACT                                                           *
*  1) REPLACE TRT BY SRST INSTRUCTION                                *
*                                       (ALBERT CHENG)   18 SEP 2014 *
**********************************************************************
SYSINCRD CSECT
         J     BEGIN
         DC    C'SYSINCRD'             * MODULE NAME
         DC    C'V1.0'                 * PROGRAM VERSION NUMBER
         DC    C'&SYSDATE'             * COMPILING DATE
         DC    C'&SYSTIME'             * COMPLIING TIME
BEGIN    BAKR  R14,0                SAVE REGISTER
         LR    R12,R15
         USING SYSINCRD,R12
DOPARM   DS    0H
         L     R3,0(R1)             PARM ADDRESS
         LH    R5,0(R3)             PARM LENGTH
         LTR   R5,R5
         JZ    NOPARM               NO PARAMETER
         OPEN  (SYSIN,OUTPUT)
         LTR   R15,R15
         JNZ   NOSYSIN              BRANCH IF RC > 0
         LA    R7,OUTPARM           OUTPUT BUFFER (12 RECORDS)
         AHI   R3,2                 PARM DATA ADDRESS
         LA    R4,0(R3,R5)          END OF PARM ADDRESS
GETPARM  DS    0H                  LOOK FOR SEMI COLON
         LA    R0,C';'                                            @01A
         LA    R9,0(R3,R5)         R9 = END OF SEARCH STRING      @01A
LOOP     SRST  R9,R3               SEARCH SEMICOLIN               @01A
         JC    1,LOOP              LOOP UNTIL SEMICOLIN IS FOUND  @01A
         JC    2,MOVELAST          BRANCH IF SEMICOLON NOT FIND   @01A
         LA    R8,1(R9)            FOUND, SAVE ADDR 1 PAST SEMI-COLON
         SR    R9,R3               CALC LENGTH OF THE PARAMETER
         AHI   R9,-1               SUBT 1 FOR EXECUTE MVC INST
         EX    R9,MVCPARM          MVC   0(0,R7),0(R3)
         PUT   SYSIN,(R7)          YES, WRITE IT
         MVI   OUTPARM,C' '        INITIALISE OUTPARM TO BLANK
         MVC   OUTPARM+1(L'OUTPARM-1),OUTPARM
         LR    R3,R8               R3 = START OF NEXT RECORD
         LR    R5,R4
         SR    R5,R8               R5 = REMAINING LENGTH
         JZ    END                 BRANCH IF END OF PARAMETER
         J     GETPARM             PROCESS NEXT PARAMETER
MOVELAST AHI   R5,-1               SUBTRACT 1 FOR MOVE
         EX    R5,MVCPARM          MOVE LAST RECORD TO OUTPRAM
         PUT   SYSIN,(R7)            WRITE RECORD
END      CLOSE SYSIN               CLOSE FILE
         XR    R15,R15             SET RC TO 0
         PR                        EXIT
NOSYSIN  WTO   'DDNAME SYSIN MISSING'
         J     QUIT
NOPARM   DS    0H
         WTO   'NO INPUT PARM'
QUIT     LA    R15,4               SET RC TO 4
         PR                        EXIT
SYSIN    DCB   DDNAME=SYSIN,MACRF=PM,DSORG=PS,BLKSIZE=80,LRECL=80,     X
               RECFM=F
OUTPARM  DS    CL80' '
MVCPARM  MVC   0(0,R7),0(R3)       MVC INPUT PARM TO OUTPARM
         YREGS
         END
