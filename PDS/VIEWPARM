/* REXX                                                           */
/* Function : View a PARMLIB member in IPLPARM                    */
/* Synatx : VIEWPARM IEASYS00                                     */
/* Author : ALBERT CHENG                                          */
/* Date   : Jan   2016                                            */
/*                                                                */
/* 1) Locate Initialization Parameter area to extract the PARMLIB */
/*    dataset name                                                */
/*    DSNAME (b 10.?+8C?+188?    CVT->ECVT->IPA                   */
/*                               IPA+1A0  ->First Parmlib         */
/* 2) Invoke ISPEXEC VIEW to browse parmlib member                */
/*                                                                */
Arg parm
NUMERIC DIGITS 10
cvt   = addc(16,'0' )                   /* fetch CVT              */
ecvt  = addc(cvt,'8c')                  /* fecth ECVT             */
ipa   = addc(ecvt,'188')                /* fetch IPA              */
parml = ipa+416                         /* ipa+x'1A0'             */
do until storage(D2X(parml),1) = " "    /* Loop until             */
  parmdsn = storage(D2X(parml), 44)     /* Extract PARMLIB dsname */
  temp = parml+3
  do i = 3 to 44
    if storage(D2X(temp), 1) = " " then leave  /* Look for blank  */
    temp = temp + 1
  end
  If parm = '' then parm = 'IEASYS00'
  parmm="'"||left(parmdsn,i)||'('||parm")'"    /* 'dsname(parm)'  */
  if sysdsn(parmm) = "OK" then do
                  address ispexec "view dataset("parmm")"
                  exit
                end
  parml = parml + 64              /* next parmlib entry */
end
say parm 'not found in parmlib'
exit
say STORAGE(D2X(parml),44)        /* Display 1st parmlib dsname   */
/* Internal Function to fetch address   */
addc: ARG ad1,ad2
RETURN C2D(STORAGE(D2X(ad1+X2D(ad2)),4))   /* ad1 = dec, ad2 = hex */

