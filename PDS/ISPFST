/* REXX ISPF status                                           */
/*                                                            */
/* Function :                                                 */
/*    Display the detail status of ISPF sessions              */
/*                                                            */
/* Syntax : ISPFST                                            */
/* Author : ALBERT CHENG                                      */
/* Date   : Jan   2016                                        */
/*                                                            */
/* Example: SWAPBAR has 6 ISPF sessions                       */
/*  VIEW   ISPFCU41  EDIT   ISPFCU41   DSLIST   MCOPY         */
/*                                                            */
/* Sample output of ISPFST                                    */
/*    TLD1 VIEW     TSOUSR1.ABCDE.SRCLIB(MEMEBRA)             */
/*    TLD2 SDSF     System log                                */
/*    TLD3 EDIT     TSOUSR1.TSO.SYSEXEC(ISPFST)               */
/*    TLD4 SDSF     EDIT    TSOUSR1A (JOB05270) JCLEDIT       */
/*    TLD5 DSLIST   EDIT      TSOUSR1.XYZ.SRCLIB(PROGABCD)    */
/*    TLD6 MCOPY    From TSOUSR1.TSO.CLIST(CLIST1)            */
/*                                                            */
tcb = ptr(540,'0')              /* PSATOLD  TCB of ISPFST     */
tcb = ptr(tcb,'84')             /* TCBOTC   ISPTASK session x */
tcb = ptr(tcb,'84')             /* TCBOTC   ISPFMAIN          */
tcb = ptr(tcb,'74')             /* TCBTCB   ISPTASK session 1 */
i   = 0
pnl.1  = 'ISRUMC2B'
pnl.2  = 'ISRUDSL0'
pnl.3  = 'ISRUTIL ';msg.3  = 'Utility Selection Panel (Option 3)'
pnl.4  = 'ISRUDA1 ';msg.4  = 'Library Utility'
pnl.5  = 'ISRUDA2S';msg.5  = 'Data Set Utility'
pnl.6  = 'ISRUARP1';msg.6  = 'Rename Data Set'
pnl.7  = 'ISRUAASE';msg.7  = 'Allocate New Data Set'
pnl.8  = 'ISRURSP ';msg.8  = 'Reset ISPF Statistics'
pnl.9  = 'ISRTSO  ';msg.9  = 'ISPF Command Shell'
pnl.10 = 'ISRUVSAM';msg.10 = 'VSAM Utilities'
pnl.11 = 'ISRSSNEW';msg.11 = 'Compare utility'
pnl.12 = 'ISRSEPRI';msg.12 = 'SuperCE (SuperC Extended) Utility'
pnl.13 = 'ISRSFPRI';msg.13 = 'Extended Search-For Utility'
pnl.14 = 'FMNSTASK';msg.14 = 'Primary Option Menu'
pnl.15 = 'FMNPDSV ';msg.15 = 'View Entry Menu'
pnl.16 = 'FMNPDSE ';msg.16 = 'Edit Entry Menu'
pnl.17 = 'FMNSTUT ';msg.17 = 'Utility Functions'
pnl.18 = 'FMNSTTS ';msg.18 = 'Tape Specific Functions'
pnl.19 = 'FMNSTDV ';msg.19 = 'Disk/VSAM Data Functions'
pnl.20 = 'FMNSTOB ';msg.20 = 'OAM Function'
pnl.21 = 'FMNSTTU ';msg.21 = 'Copybook and Template Utility function'
pnl.22 = 'FMNSHFS ';msg.22 = 'Access Hierarchical File System'
pnl.23 = 'FMNSTMQ ';msg.23 = 'WebSphere MQ Function'
pnl.24 = 'FMN3ST01';msg.24 = 'CICS Primary Option Menu'
pnl.25 = 'FMNPDVT ';msg.25 = 'Display VTOC'
pnl.26 = 'FMNPDMB ';msg.26 = 'Memory browse'
pnl.27 = 'FMNPFCH ';msg.27 = 'Find/Change Utility'
pnl.28 = 'FMNPVLM ';msg.28 = 'Load Module Information'
pnl.29 = 'FMNPVLMB';msg.29 = 'Load Module Information'
pnl.30 = 'FMNPCLM ';msg.30 = 'Load Module Compare'
pnl.31 = 'FMNPSEL ';msg.31 = 'WebSphere MQ Managers'
pnl.32 = 'ISRUUDL0';msg.32 = 'z/OS UNIX Directory List'
pnl.33 = 'ISRDDNP ';msg.33 = 'Main Menu'
pnl.34 = 'ISRUDLP ';msg.34 = 'Data Set List Utility'
pnl.35 = 'BPXWP99 ';msg.35 = 'UNIX System Services ISPF Shell'
pnl.36 = 'ISPYFP  ';msg.36 = 'Invoke dialog functions/selection panel'
pnl.37 = 'ISPYP1  ';msg.37 = 'Display panel'
pnl.38 = 'ISRDDBRS';msg.38 = 'Browse storage'
pnl.39 = 'ISPYVPN ';msg.39 = 'Variables'
pnl.40 = 'ISPYTPI ';msg.40 = 'Tables'
pnl.41 = 'ISPYLP1 ';msg.41 = 'Browse ISPF log'
pnl.42 = 'ISPYS3  ';msg.42 = 'Invoke Dialog Service'
pnl.43 = 'ISPUCMA ';msg.43 = 'Command Table Utility'
pnl.44 = 'ISPUCME ';msg.44 = 'Display coammand table'
pnl.45 = 'ERB0PRM ';msg.45 = 'RMF main menu'
pnl.46 = 'ERB3PRM ';msg.46 = 'RMF Monitor III Primary Menu'
pnl.47 = 'DGTSMMD1';msg.47 = 'ISMF Primary Option Menu'
pnl.48 = 'DGTDDDS1';msg.48 = 'Data set Selection Entry Panel'
pnl.49 = 'DGTSCMC1';msg.49 = 'Management Class Application Selection'
pnl.50 = 'DGTSCDC1';msg.50 = 'Data Class Application Selection'
pnl.51 = 'DGTLGP11';msg.51 = 'ISMF Data set list'
pnl.52 = 'XXXXXXXX';msg.52 = '???'
pnlno = 52
spnl.1  = 'ISFJE   '
spnl.2  = 'ISFJTOC ';smsg.2  = 'SDSF PRIMARY OPTION MENU'
spnl.3  = 'ISFJD   ';smsg.3  = 'DA (Actice users)'
spnl.4  = 'ISFJL   ';smsg.4  = 'System log'
spnl.5  = 'ISFJH   ';smsg.5  = 'Held output queue'
spnl.6  = 'ISFJO   ';smsg.6  = 'Output queue'
spnl.7  = 'ISFJI   ';smsg.7  = 'Input queue'
spnl.8  = 'ISFJS   ';smsg.8  = 'Status of jobs'
spnl.9  = 'ISFJN   ';smsg.9  = 'Initiators'
spnl.10 = 'ISFJP   ';smsg.10 = 'Printers'
spnl.11 = 'ISFJG   ';smsg.11 = 'Job classes'
spnl.12 = 'ISFJK   ';smsg.12 = 'Nodes'
spnl.13 = 'ISFJR   ';smsg.13 = 'Lines'
spnl.14 = 'ISFJZ   ';smsg.14 = 'Scheduling environments'
spnl.15 = 'ISFJ0EN ';smsg.15 = 'Enclaves'
spnl.16 = 'ISFJ0PS ';smsg.16 = 'OMVS processes'
spnl.17 = 'ISFJ0R  ';smsg.17 = 'Resource monitor'
spnl.18 = 'ISFJ0Z  ';smsg.18 = 'Health Checker'
spnlno = 18
do until tcb = 0
  rb     = ptr(tcb,'0')              /* RB                         */
  if storage(D2X(rb+96),7) \= 'ISPTASK' then
    do
     do until storage(D2X(rb+96),7) = 'ISPTASK'
      if storage(D2X(rb+96),5) = 'EXEC ' then leave
      if storage(D2X(rb+96),4) = 'PDS ' then leave
      rb = c2d(storage(D2X(rb+29),3))   /* Previous RB             */
      if rb = 0 then exit
     end
    end
  tld    = ptr(rb, '44')
  i      = i + 1
  tlds   = tldx                      /*                            */
  tldx   = storage(D2X(tld), 4)      /* Get TLDx                   */
  tldc.i = tlds                      /* Save old TLD               */
  sa     = ptr(tld,'80')             /* Address of screen area     */
  line3d = sa + 161
  line3  = storage(D2X(line3d),63)
  if left(tldx,3) = 'TLD' then do
   temp1 = sa + 168
   do until storage(D2X(temp1),1) > 'C0'x
    temp1 = temp1 + 1
   end
   temp2 = temp1+1
   do until storage(D2X(temp2),1) < '.'
    temp2 = temp2 + 1
   end
   panel    = storage(D2X(tld+344),8)
   scrname  = C2D(storage(D2X(tld+X2D(354)),1))  /*         */
   command  = storage(D2X(tld+X2D(354)),8)       /* SCRNAME */
   line     = storage(D2X(temp1),temp2-temp1)
   if command = 'DSLIST' & panel \= 'ISRUDSL0' then ,
      line = storage(D2X(sa+161),55)
   msg.1 = storage(D2X(sa+170),52)
   msg.2 = storage(D2X(sa+170),59)
   do a = 1 to pnlno
    if panel = pnl.a then do
       line = msg.a
       leave
       end
   end
   if a = pnlno then tldout = 'PANEL : ' || panel
                else tldout = panel || ' ' || line
   if left(command, 5) = 'RACF ' then line = storage(D2X(sa+28),42)
   smsg.1 = storage(D2X(sa+6),44)
   if left(panel, 3) = 'ISF' then
        do
          helppnl = storage(D2X(tld+360), 8)
          tldout = line3
          do a = 1 to spnlno
             if helppnl = spnl.a then do
                 tldout = 'SDSF     ' || smsg.a
                 leave
             end
          end
        end
   if scrname = 0 then line = tldx tldout
                  else line = tldx command line
   display = 0
   do j = 1 to i
    if tldx = tldc.j then display = 1
   end
   if display = 0 then say line
 tcb    = ptr(tcb,'74')          /* TCBTCB   Mother TCB  */
end
end
exit
/* Internal Function to fetch address   */
ptr: ARG ad1,ad2
RETURN C2D(STORAGE(D2X(ad1+X2D(ad2)),4))   /* ad1 = dec, ad2 = hex */
