***********************************************************************
*
*        LPALIB IS THE SUB-PROGRAM OF LOCATE.
*        IT LOCATES A MODULE IN LPA LIBRARIES. SET RETURN CODE TO 0
*        IF THE MODULE EXISTS. SET RETURN CODE TO 4 IF MODULE DOES
*        NOT EXIST.
* INPUT
*        R1  -- ADDRESS OF THE COMMAND
*        R2  -- ADDRESS OF LPA DATASET NAME
* OUTPUT
*        R15 -- RETURN CODE
*
* ABSTRACT
*
***********************************************************************
LPALIB   CSECT
         BAKR  R14,0             SAVE REGISTERS INTO LINKAGE STACK
         J     START                  BRANCH AROUND MODULE
         DC    C'LPALIB.V01'           DESCRIPTION AND VERSION NO.
         DC    C'&SYSDATE &SYSTIME'   ASSEMBLE DATE AND TIME
START    DS    0H
         LR    R12,R15
         USING LPALIB,R12             ESTABLISH PROGRAM BASE REG
*        LA    R1,BLDLNAME
         MVC   BLDLNAME,0(R1)
         L     R4,16
         L     R4,CVTSMEXT-CVT(,R4)   STORAGE MAP EXTENSION
         SAM31
         L     R4,X'38'(,R4)          EPLPS
         L     R5,4(,R4)              LPATCNT
         AHI   R4,8
NEXT_LPA_ENTRY DS  0H
         SAM31
         MVC   ALC_DSNAME,1(R4)
         LA    R1,ALC_RBP
         DYNALLOC                     ALLOCATE DDNAME TEMPLPA
         LTR   R15,R15
         JNZ   EXIT4
         SAM24
         OPEN  (TEMPLPA,(INPUT))      OPEN LPA LIBRARY
         LTR   R15,R15                CHECK RETURN CODE
         JNZ   EXIT4                  BRANCH IF OPEN ERROR
         BLDL  TEMPLPA,LIST           LOCATE MODULE
         LTR   R15,R15                CHECK RETURN CODE
         JNZ   CLOSEFL                BRANCH IF MEMBER NOT FIND
         MVC   0(44,R2),ALC_DSNAME    GET LPA DATASET
*        TPUT  ALC_DSNAME,44
         J     EXIT
CLOSEFL  CLOSE TEMPLPA                CLOSE TEMPLPA
         LA    R1,UA_RBP
         DYNALLOC                     UN-ALLOCATE DDNAME TEMPLPA
         AHI   R4,45
         BRCT  R5,NEXT_LPA_ENTRY      NEXT LPAT ENTRY
EXIT4    DS    0H
         LA    R15,4                  SET ERROR RETURN CODE
         PR                           RETURN
EXIT     DS    0H
         MVC   0(44,R2),ALC_DSNAME    GET LPA DATASET NAME
         CLOSE TEMPLPA
         LA    R1,UA_RBP
         DYNALLOC                     UN-ALLOCATE DDNAME TEMPLPA
         SR    R15,R15
         PR                           RETURN
TEMPLPA  DCB   DDNAME=X$1$2$3Y,MACRF=R,DSORG=PO,EODAD=EXIT4
*-------------------------------------------------------------------
LIST     DC    H'1',H'14'              BLDL LIST
BLDLNAME DC    CL8' '                  BLDL NAME OF MODULE
         DC    XL3'00'                 TTR OF MODULE
         DC    X'00'                   CONCATENATION NUMBER
         DC    X'00'                   LOCATION
         DC    X'00'                   ENTRY TYPE
*-------------------------------------------------------------------
*---------------------------------------------------------------------*
*              DYNAMIC ALLOCATE REQUEST BLOCK                         *
*---------------------------------------------------------------------*
ALC_RBP     DC  A(ALC_RB+X'80000000')
*
ALC_RB      DS  0F
            DC  AL1(20),X'01'          DSNAME ALLOCATION VERB CODE
            DC  H'0'                   ALLOCATION FLAGS1
ALC_ERCD    DC  H'0'                   ERROR CODE
ALC_INFO    DC  H'0'                   INFORMATION CODE
            DC  A(ALC_TXTS)            TEXT UNIT POINTER
            DC  F'0'                   REQUEST BLOCK EXTENSION
            DC  F'0'                   ALLOCATION FLAGS2
ALC_TXTS    DC  A(ALC_TXT1)
            DC  A(ALC_TXT2)
            DC  A(ALC_TXT3+X'80000000')
*
ALC_TXT1    DS  0F                      DDNAME TEXT UNIT
            DC  XL2'01',XL2'01',AL2(8)  KEY,#,LENGTH
ALC_DDNAME  DC  CL8'X$1$2$3Y'           DDNAME
*
ALC_TXT2    DS  0F                      DSNAME TEXT UNIT
            DC  XL2'02',XL2'01',AL2(44) KEY,#,LENGTH
ALC_DSNAME  DS  CL44                    DSNAME
*
ALC_TXT3    DS  0F                      DISP TEXT UNIT
            DC  H'4',H'1',H'1',X'08'    KEY,#,LENGTH,DISP=SHR
*---------------------------------------------------------------------*
*              DYNAMIC UN-ALLOCATE REQUEST BLOCK                      *
*---------------------------------------------------------------------*
         DS   0F
UA_RBP   DC   A(UA_RB+X'80000000')
UA_RB    DS   0F
         DC   AL1(20),X'02'           UN-ALLOCATION VERB
         DC   H'0'                    DE-ALLOCATION FLAGS1
UA_ERCD  DC   H'0'
UA_INFO  DC   H'0'
         DC   A(UA_TXTS)              TEXT POINTER
         DC   F'0'                    REQUEST BLOCK EXTENSION
         DC   F'0'                    DE-ALLOCATION FLAGS2
UA_TXTS  DC   A(UA_TXT1)
         DC   A(UA_TXT2+X'80000000')
UA_TXT1  DS   0F                      DDNAME TEXT UNIT
         DC   XL2'07',XL2'00'         KEY,#
UA_TXT2  DS   0F                      DDNAME TEXT UNIT
         DC   XL2'01',XL2'01',AL2(8)  KEY,#,LENGTH
         DC   CL8'X$1$2$3Y'
         LTORG
         CVT DSECT=YES,LIST=NO
         YREGS
         END
