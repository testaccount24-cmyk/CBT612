SRCHLOG  TITLE 'SYSLOG SEARCH UTILITY'
*
*---------------------------------------------------------------------*
*  AUTHOR  - ALBERT CP CHENG                                          *
*                                                                     *
*  SRCHLOG - SEARCH SYSLOG WITH RECFM VBA/VB AND SEARCH STRINGS       *
*            ACCORDING TO THE PARAMETERS FROM SYSIN                   *
*            1) ONE SEARCH STRING PER INPUT LINE                      *
*            2) THE LENGTH OF EACH STRING CANNOT EXCEED 40.           *
*            3) SPACE IS ALLOWED IN THE SEARCH STRING AND THE DEFAULT *
*               DELIMITER IS #.                                       *
*            4) THE MAXIMUM NUMBER OF SEARCH STRINGS ARE 250.         *
*            5) READ MULTIPLE LINES OF SYSLOG AS 1 MESSAGE. IF THE    *
*               STRING IS FOUND, THE ENTIRE MESSAGE (INCLUDING ALL    *
*               THE MULTIPLE LINES) ARE DISPLAYED.                    *
*            6) THE LIMIT OF MULTIPLE LINES IS 200.                   *
*            7) RETURN CODE 4 MEANS PROCESSING IS CONTIUNED           *
*            8) RETURN CODE 8 MEANS THE PROCESSING IS ABORTED         *
*            9) RECFM OF DDNAME SYSLOG/PRTFILE IS VBA                 *
*           10) LRECL OF DDNAME SYSLOG/PRTFILE IS 135                 *
*           11) MSG IEE174I/$HASP881 HAVE MORE THAN 200               *
*               MULTIPLE LINES                                        *
*           12) THE DEFAULT RECFM IS VBA                              *
*                                                                     *
*       RC    MESSAGE                                                 *
*       8    'ERROR - SEARCH STRING LENGTH > 40 OR NO DELIMITER'      *
*       8    'ERROR - NUMBER OF SEARCH STRINGS > 250'                 *
*       8    'ERROR - NO SEARCH PARAMETERS'                           *
*       8    'ERROR - RECFM SHOULD BE A OR B'                         *
*       4    'ERROR - SKIP SEARCHING MULTIPLE LINES BEYOND 200'       *
*       4    '                   STRING NOT FOUND !!!'                *
*                                                                     *
*       JCL - //PRINT   EXEC PGM=SRCHLOG,PARM='!B'                    *
*             //STEPLIB   DD DSN=YOUR.LOADLIB,DISP=SHR                *
*             //SYSLOG    DD DSN=SYSLOG.DATASET.NAME,DISP=SHR         *
*             //PRTFILE   DD SYSOUT=*                                 *
*             //SYSIN     DD *                                        *
*             IEC161I 072-053!                                        *
*             COMPLETION CODE=4039!                                   *
*             //                                                      *
*                                                                     *
* ENHANCEMENT :                                                       *
*   1) SET THE LIMIT OF MULTIPLE LINES TO 200 AND        23.05.2018   *
*      IGNORES SEARCH THE STRING BEYOND LINE 200.                     *
*   2) 1ST PARAMETER IS DELIMITER                        31.05.2018   *
*      ACCEPT DELIMITER FROM PARAMETER AND THE                        *
*      DEFAULT DELIMITER IS #                                         *
*      THE ACCEPTED DELIMITER IS @#$%&                                *
*   3) 2ND PARAMETER IS SYSLOG RECFM                     25.12.2019   *
*      SUPPORT SYSLOG WITH RECFM VB AND VBA                           *
*      THE DEFAULT RECFM IS VBA                                       *
*      THE ACCEPTED PARAMETER IS EITHER A OR B                        *
*      A MEANS RECFM VBA AND MEANS RECFM VB                           *
*---------------------------------------------------------------------*
         YREGS
SRCHLOG  CSECT
         J     GETPARM                 *
         DC    C'&SYSDATE &SYSTIME'
GETPARM  DS    0H                      *
         BAKR  R14,0                   * SAVE R0-R15 TO LINKAGE STACK
         LR    R12,R15                 * ESTABLISH ADDRESSIBILITY
         USING SRCHLOG,R12             * TO R12 (IE R12 IS BASE REG)
         USING TBLREC,R9               *
         L     R1,0(R1)                * GET PARM LIST ADDR
         LH    R15,0(R1)               * LOAD PARM LENGTH
         LTR   R15,R15                 * PARAMETER LENGTH = 0 ??
         JZ    OPENFILE                * BRANCH IF ZERO
         MVC   DELIMIT(2),2(R1)        * MOVE DELIMITER            @03C
OPENFILE OPEN  (SYSLOG,(INPUT),SYSIN,(INPUT),PRINT,(OUTPUT))
         LTR   R15,R15                 * OPEN FILE ERROR ?
         JNZ   EXIT                    * EXIT IF RC IS NON ZERO
         CLI   RECFM,C'A'                                          @03A
         JE    GETSTOR                                             @03A
         CLI   RECFM,C'B'                                          @03A
         JNE   ERROR5                                              @03A
GETSTOR  STORAGE OBTAIN,LENGTH=37520   * 250*41 (10250) 135*202(27270)
         LR    R7,R1                   * SAVE ADDRESS OF SEARCH STR TBL
         ST    R1,SRCHTBL              * ADDRESS OF SEARCH STRING TABLE
         SR    R11,R11                 * CLEAR R11
         AHI   R1,10250                *
         ST    R1,LOGTBL               * ADDRESS OF LOGREC TABLE
READPRM  GET   SYSIN,INBUF             * READ A RECORD FROM SYSIN
         AHI   R11,1                   * INCREMENT # OF SEARCH STRINGS
         CHI   R11,250                 * # OF SEARCH STRINGS > 250
         JH    ERROR2                  * BRANCH IF > 250
         SR    R0,R0                   * CLEAR R0 FOR IC INSTRUCTION
         IC    R0,DELIMIT              * R0 IS SEARCH STRING
         LA    R4,INBUF                * R4 -> STRING ADDRESS
         LA    R5,41                   * R5 -> STRING LENGTH
         LA    R3,0(R4,R5)             * R3 -> END OF STRING
LOOP     SRST  R3,R4                   * SEARCH DELIMETER
         JC    1,LOOP                  * LOOP UNTIL DELIMERER IS FOUND
         JC    2,ERROR1                * DISPLAY ERROR MSG IF NOT FOUND
         SR    R3,R4                   * CALC LENGTH OF THE PARAMETER
         AHI   R3,-1                   * SUBT 1 FOR EXECUTE MVC INST
         EX    R3,MVCPARM              * MVC  1(0,R7),INBUF
         STC   R3,0(R7)                * SAVE LENGTH OF SEARCH STRING
         AHI   R7,41                   * POINT TO NEXT SEARCH STRING
         J     READPRM                 * READ NEXT RECORD
EOP      CLOSE SYSIN                   * CLOSE SYSIN
         LTR   R11,R11                 * SAVE NUMBER OF SEARCH PARMS
         JZ    ERROR3                  * BRANCH IF NO INPUT DATA
         STH   R11,ENTRY#              * SAVE NUMBER OF SEARCH PARMS
         PUT   PRINT,TITLE1            * PRINT TITLE LINE
         PUT   PRINT,BLANKS            * PRINT BLANK LINE
*
LOG000   DS    0H                      * LOAD MULTIPLE SYSLOG LOGREC
         LHI   R7,200                  *      INTO LOGTBL
         L     R9,LOGTBL               * ADDRESS OF LOG RECORD TABLE
         CLI   SWITCH,C'Y'             *
         JE    LOG200                  *
         MVC   0(135,R9),LOGREC        * MOVE LAST LOGREC TO
         AHI   R9,135                  *    1ST ENTRY OF LOGREC TABLE
LOG100   MVC   LOGREC,BLANKS           * CLEAR RECORD
         GET   SYSLOG,LOGREC           * GET SYSLOG RECORD
LOG200   MVC   0(135,R9),LOGREC        *
         MVI   SWITCH,C'N'             * OFF FIRST SWITCH
         CLI   RECFM,C'B'              * RECFM =                   @03A
         JE    LOG300                  * BRANCH IF YES             @03A
         CLI   RECORD+1,C'N'           * NORMAL LINE ? VBA
         JE    ENDMULT                 * BRANCH IF YES
         CLI   RECORD+1,C'M'           * MULTIPLE LINE HEADER VBA
         JE    ENDMULT                 * BRANCH IF MULTIPLE RECORD
         J     LOG350                  * BRANCH IF YES             @03A
LOG300   CLI   RECORD,C'N'             * NORMAL LINE VB ?          @03A
         JE    ENDMULT                 * BRANCH IF YES             @03A
         CLI   RECORD,C'M'             * MULTIPLE LINE HEADER VB   @03A
         JE    ENDMULT                 * BRANCH IF MULTIPLE RECORD @03A
LOG350   AHI   R9,135                  *
         JCT   R7,LOG100               *
         MVC   LOGREC(136),BLANKS      *
         PUT   PRINT,LOGREC            *
         LHI   R0,L'EMSG4              *
         AHI   R0,8                    *
         STH   R0,LOGREC               * SAVE RECORD LENGTH
         MVC   LOGREC+8(L'EMSG4),EMSG4 * WRITE ERROR MESSAGE EMSG4
         PUT   PRINT,LOGREC            *
         LHI   R15,4                   * SET RETURN CODE TO 4
         STH   R15,RETCODE             * SAVE RETURN CODE
* IGNORES MULTIPLE LINES MORE THAN 200
         L     R9,LOGTBL
         PUT   PRINT,0(R9)             * WRITE CURRENT RECORD
         MVC   LOGREC(136),BLANKS      *
         PUT   PRINT,LOGREC            * WRITE A BLANK LINE
LOG400   MVC   LOGREC,BLANKS           * CLEAR RECORD
         GET   SYSLOG,LOGREC           * GET SYSLOG RECORD
         CLI   RECFM,C'B'              * RECFM VB                  @03A
         JE    LOG500                  * BRANCH IF YES             @03A
         CLI   LOGVBA,C'N'             * NORMAL LINE (VBA)
         JE    ENDMULT                 * BRANCH IF YES
         CLI   LOGVBA,C'M'             * MULTIPLE LINE HEADER (VBA)
         JNE   LOG400                  * BRANCH IF MULTIPLE RECORD
         J     ENDMULT                                             @03A
LOG500   CLI   LOGVB,C'N'              * NORMAL LINE (VB)          @03A
         JE    ENDMULT                 * BRANCH IF YES             @03A
         CLI   LOGVB,C'M'              * MULTIPLE LINE HEADER (VB) @03A
         JNE   LOG400                  * BRANCH IF MULTIPLE RECORD @03A
ENDMULT  STH   R7,LINECT               * NEW MESSAGE LINE
         LHI   R4,201                  *
         SH    R4,LINECT               * # OF MULTIPLE LINES
         STH   R4,LINECT               * NUMBER OF LOGREC
         L     R9,LOGTBL               * ADDRESS OF LOG RECORD TABLE
SRCH20   LH    R11,ENTRY#              * NUMBER OF SEARCH PARMS
         L     R7,SRCHTBL              * ADDRESS OF SEARCH STRING TABLE
         LH    R6,RECLEN               * LOAD RECORD LENGTH
         CHI   R6,62                   * SHORT RECORD ??
         JL    SRCH70                  * BRANCH IF SHORT RECORD
         AHI   R6,-44                  *
         LR    R8,R6                   * SAVE R6 TO R8
SRCH30   LA    R5,RECORD+56            * R5 -> RECORD
         SR    R10,R10                 * CLEAR R10 FOR IC INSTR
         IC    R10,0(R7)               * GET LENGTH OF SEARCH PARM
         LR    R6,R8                   * RESTORE R6
SRCH50   EX    R10,COMPARM             * CLC  1(0,R7),0(R5)
         JE    PUTLOG                  * EQ THEN WRITE THE RECORD
         LA    R5,1(R5)                * INCR. POSITION IN LOG RECORD
         JCT   R6,SRCH50               * SEARCH CHARACTER BY CHARACTER
         AHI   R7,41                   * NEXT SEARCH STRING
         JCT   R11,SRCH30              * SEARCH
SRCH70   AHI   R9,135                  * POINT TO NEXT LOG TABLE ENTRY
         JCT   R4,SRCH20               * PROCESS NEXT LOG TABLE ENTRY
         J     LOG000                  * CONTINUE
PUTLOG   LH    R4,LINECT               * LOAD # OF MULTIPLE LOG REC
         L     R9,LOGTBL               * ADDRESS OF LOG RECORD TABLE
PUTLOG2  PUT   PRINT,0(R9)             * WRITE LOG RECORD TABLE
         AHI   R9,135                  * POINT TO NEXT ENTRY
         JCT   R4,PUTLOG2              * PROCESS NEXT ENTRY
         MVI   EMPTY,C'N'              * SET SWITCH TO NOT EMPTY
         J     LOG000                  * CONTINUE
ABEND    DC    X'0000'
*---------------------------------------------------------------------*
* ERROR MESSAGES                                                      *
*---------------------------------------------------------------------*
ERROR1   PUT   PRINT,TITLE1            * WRITE TITLE
         MVC   LOGREC(136),BLANKS      *
         PUT   PRINT,LOGREC            * WRITE A BLANK LINE
         LHI   R0,L'EMSG1              * LOAD THE LENGTH OF EMSG1
         AHI   R0,8                    *
         STH   R0,LOGREC               * SAVE RECORD LENGTH
         MVC   LOGREC+8(L'EMSG1),EMSG1 *
         PUT   PRINT,LOGREC            * WRITE ERROR MESSAGE EMSG1
         MVC   LOGREC(136),BLANKS      *
         PUT   PRINT,LOGREC            *
         MVC   LOGREC+5(L'INBUF),INBUF *
*        PUT   PRINT,LOGREC            * PRINT ERROR PARAMETER
         J     CC8                     * SET RETURN CODE 8 AND ABORT
ERROR2   PUT   PRINT,TITLE1            * WRITE TITLE
         MVC   LOGREC(136),BLANKS      *
         PUT   PRINT,LOGREC            * WRITE A BLANK LINE
         LHI   R0,L'EMSG2              * LOAD THE LENGTH OF EMSG2
         AHI   R0,8                    *
         STH   R0,LOGREC               * SAVE RECORD LENGTH
         MVC   LOGREC+8(L'EMSG2),EMSG2 *
*        PUT   PRINT,LOGREC            *
         J     CC8                     * SET RETURN CODE 8 AND ABORT
ERROR3   PUT   PRINT,TITLE1            * WRITE TITLE
         MVC   LOGREC(136),BLANKS      *
         PUT   PRINT,LOGREC            * WRITE A BLANK LINE
         LHI   R0,L'EMSG3              * LOAD THE LENGTH OF EMSG3
         AHI   R0,8                    *
         STH   R0,LOGREC               * SAVE RECORD LENGTH
         MVC   LOGREC+8(L'EMSG3),EMSG3 * WRITE ERROR MESSAGE EMSG3
*        PUT   PRINT,LOGREC            *
         J     CC8                     * SET RETURN CODE 8 AND ABORT
ERROR5   PUT   PRINT,TITLE1            * WRITE TITLE               @03A
         MVC   LOGREC(136),BLANKS      *                           @03A
         PUT   PRINT,LOGREC            * WRITE A BLANK LINE        @03A
         LHI   R0,L'EMSG5              * LOAD THE LENGTH OF EMSG5  @03A
         AHI   R0,8                    *                           @03A
         STH   R0,LOGREC               * SAVE RECORD LENGTH        @03A
         MVC   LOGREC+8(L'EMSG5),EMSG5 * WRITE ERROR MESSAGE EMSG5 @03A
         LHI   R15,8                   * SET RETURN CODE TO 8      @03A
         PUT   PRINT,LOGREC            *                           @03A
         PR                            *                           @03A
CC8      PUT   PRINT,LOGREC            *
         LHI   R15,8                   * SET RETURN CODE TO 8
         STH   R15,RETCODE             * SAVE RETURN CODE
         J     CLOSEFL                 * ABORT
*---------------------------------------------------------------------*
* END ROUTINES                                                        *
*---------------------------------------------------------------------*
EOF      CLI   EMPTY,C'N'              * STRING NOT FIND
         JE    CLOSEFL                 * BRANCH IF YES
         PUT   PRINT,BLANKS            *
         PUT   PRINT,BLANKS            *
         PUT   PRINT,NFIND             * DISPLAY STRING NOT FOUND
         LHI   R15,4                   * SET RETURN CODE TO 4
         STH   R15,RETCODE             * SAVE RETURN CODE
CLOSEFL  CLOSE (PRINT,,SYSLOG)         * CLOSE SYSLOG
         STORAGE RELEASE,LENGTH=37520,ADDR=SRCHTBL
         LH    R15,RETCODE             * RESTORE RETURN CODE
EXIT     PR                            * RETURN TO ZOS
MVCPARM  MVC   1(0,R7),INBUF           * MOVE PARM FROM INBUF
COMPARM  CLC   1(0,R7),0(R5)           * COMPARE NAME TO RECORD ?
*
*---------------------------------------------------------------------*
* CONSTANTS AND STORAGE AREAS                                         *
*---------------------------------------------------------------------*
EMSG1    DC    C'ERROR - SEARCH STRING LENGTH > 40 OR NO DELIMITER'
EMSG2    DC    C'ERROR - NUMBER OF SEARCH STRINGS > 250'
EMSG3    DC    C'ERROR - NO SEARCH PARAMETER'
EMSG4    DC    C'SKIP SEARCHING THE FOLLOWING LINE ( > 200)'
EMSG5    DC    C'ERROR - RECFM SHOULD BE A OR B'                   @03A
DELIMIT  DC    C'#'                    * DEFAULT DELIMITER AND VBA
RECFM    DC    C'A'                    * DEFAULT RECFM A  -> VBA
*                                                      B  -> VB
SRCHTBL  DC    A(0)                    * ADDRESS OF SEARCH STRING TABLE
LOGTBL   DC    A(0)                    * ADDRESS OF LOGREC TABLE
ENTRY#   DC    H'0'                    * NUMBER OF SEARCH PARAMETERS
LINECT   DC    H'0'                    * NUMBER OF MULTIPLE LINES
RETCODE  DC    H'0'                    * RETURN CODE
EMPTY    DC    C'Y'                    *
SWITCH   DC    C'Y'                    * FIRST TIME SWITCH
*
*   STORAGE DEFINING INPUT RECORD FROM SYSLOG DATASET
*
INBUF    DS    CL80                    * SYSIN RECORD
LOGREC   DC    H'135'                  * RECORD LENGTH
         DC    H'00'
LOGVB    DC    C' '                    * VB  START
LOGVBA   DC    CL130' '                * VBA START
*
TITLE1   DC    H'122'
         DC    H'0'
         DC    CL16' '
         DC    CL40'*--------------------------------------*'
         DC    CL22'   SEARCH SYSLOG      '
         DC    CL40'*--------------------------------------*'
NFIND    DC    H'47'
         DC    H'0'
         DC    CL22' '
         DC    CL21'STRING NOT FOUND !!! '
BLANKS   DC    H'135',H'0',CL131' '
*
*---------------------------------------------------------------------*
* DCB'S                                                               *
*---------------------------------------------------------------------*
*
*    INPUT SYSLOG
*
SYSLOG   DCB   DDNAME=SYSLOG,                                          C
               MACRF=GM,                                               C
               DSORG=PS,                                               C
               RECFM=VBA,                                              C
               LRECL=135,                                              C
               EODAD=EOF
*
*    INPUT SYSIN
*
SYSIN    DCB   DDNAME=SYSIN,                                           C
               MACRF=GM,                                               C
               DSORG=PS,                                               C
               RECFM=FB,                                               C
               LRECL=80,                                               C
               EODAD=EOP
*
*   OUTPUT FILES
*
PRINT    DCB   DDNAME=PRTFILE,                                         C
               MACRF=PM,                                               C
               DSORG=PS,                                               C
               RECFM=VBA,                                              C
               LRECL=135
TBLREC   DSECT
         DS    0CL135                * SYSLOG RECORD
RECLEN   DS    CL2                   * 2 BYTES RECORD LENGTH
         DS    CL2                   * 2 BYTES BLOCK LENGTH
RECORD   DS    CL132                 * FILLER
         LTORG
         END
